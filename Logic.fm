import Data.Empty open
import Data.Or open
import Data.And open

// The Logic namespace articulates the constructive-logical interpretation of
// mathematics and computation. In other words, the "Howard" component of the
// Curry-Howard-Lambek correspondence.

// Logic is included in the Formality standard library primarily for educational
// and historical reasons, and is not intended to be a practical aid to
// computation. That is, by analogy with embyrological parallelism, the authors
// intend for Formality's ontogeny to at least partially recapitulate its
// phylogeny. https://en.wikipedia.org/wiki/Recapitulation_theory

// If and only if
Iff : {A : Type, B : Type} -> Type
  And(A -> B, B -> A)

// Modus ponens
affirms : {A : Type, B : Type, implies : A -> B, a : A} -> B
  implies(a)

// Modus tollens
denies : {A : Type, B : Type, implies : A -> B, nb : Not(B)} -> Not(A)
  {x} nb(implies(x))

// Disjunctive syllogism (modus tollendo ponens)
or_syllogism : {~A : Type, ~B : Type, case or_AB : Or(A,B), neg_A : Not(A)} -> B
| left  => absurd(neg_A(or_AB.value), B)
| right => or_AB.value

// Hypothetical syllogism
logical_chain_rule : {~A : Type, ~B : Type, ~C : Type, f : A -> B, g : B -> C, x : A} -> C
  g(f(x))
