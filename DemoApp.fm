import App 

// A demo App that displays screen coordinates
T DemoAppState
| demoappstate(
  mousex  : Number,
  mousey  : Number,
  clicks  : Number,
  lastkey : Number
)

demo_app : App(DemoAppState)
  // Application state
  let state = demoappstate(0, 0, 0, 0)

  // Application interface
  let render = (state) =>
    case state
    | demoappstate =>
      cpy state.mousex = state.mousex
      cpy state.mousey = state.mousey

      box("div", <Prop>{"border": "1px solid black"}, <Doc>[
        // Title
        box("h2", <Prop>{}, <Doc>[txt("Formality Demo App")]),

        // Mouse position
        box("h3", <Prop>{}, <Doc>[txt("Mouse position:")]),
        box("ul", <Prop>{}, <Doc>[
          box("li", <Prop>{}, <Doc>[txt("x:"), num(state.mousex)]),
          box("li", <Prop>{}, <Doc>[txt("y:"), num(state.mousey)])
        ]),

        // Click count
        box("h3", <Prop>{}, <Doc>[txt("Mouse clicks:")]),
        box("div", <Prop>{}, <Doc>[num(state.clicks)]),

        // Last key
        box("h3", <Prop>{}, <Doc>[txt("Last key press:")]),
        box("div", <Prop>{}, <Doc>[num(state.lastkey)]),

        // An image
        img([64, 64], (pos) => 
          get [x,y] = pos
          let r = ((x .*. 8 .+. state.mousex) .%. 256) .|. 0
          let g = ((y .*. 8 .+. state.mousey) .%. 256) .|. 0
          let b = 128
          let c = 0xFF000000
          let c = c .|. (b .<<. 16)
          let c = c .|. (g .<<. 8)
          let c = c .|. r
          c),
      ])
    : Doc

  // Application interaction
  let update = (event, state) =>
    case event
    + state : DemoAppState
    | mouseclick =>
      case state
      | demoappstate => demoappstate(state.mousex, state.mousey, state.clicks .+. 1, state.lastkey)
      : DemoAppState
    | mousemove =>
      case state
      | demoappstate => demoappstate(event.x, event.y, state.clicks, state.lastkey)
      : DemoAppState
    | keypress => 
      case state
      | demoappstate => demoappstate(state.mousex, state.mousey, state.clicks, event.key)
      : DemoAppState
    | keydown => state
    | keyup => state
    : DemoAppState

  // Builds the application
  app(~DemoAppState, state, render, update)
